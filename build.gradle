buildscript {
	ext {
		springBootVersion = '2.0.0.RC2'
		postgresVersion = '42.2.1'
		jooqVersion = '2.0.11'
		flywayVersion = '5.0.7'
	}
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
		classpath "org.postgresql:postgresql:${postgresVersion}"
		classpath "nu.studer:gradle-jooq-plugin:${jooqVersion}"
		classpath "gradle.plugin.com.boxfuse.client:gradle-plugin-publishing:${flywayVersion}"
	}
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'nu.studer.jooq'
apply plugin: 'org.flywaydb.flyway'

group = 'be.inniger'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 9

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/milestone" }
}

dependencies {
	compile 'org.springframework.boot:spring-boot-starter-actuator'
	compile 'org.springframework.boot:spring-boot-starter-jooq'
	compile 'org.springframework.boot:spring-boot-starter-web'
	compile 'org.flywaydb:flyway-core'
	runtime 'org.postgresql:postgresql'
	jooqRuntime 'org.postgresql:postgresql'
	compileOnly 'org.projectlombok:lombok'
	testCompile 'org.springframework.boot:spring-boot-starter-test'
}

jooq {
	homepage(sourceSets.main) {
		jdbc {
			driver = 'org.postgresql.Driver'
			url = 'jdbc:postgresql://localhost:5432/homepage'
			user = 'bram'
			password = ''
		}
		generator {
			name = 'org.jooq.util.DefaultGenerator'
			database {
				inputSchema = 'public'
			}
			strategy {
				name = 'org.jooq.util.DefaultGeneratorStrategy'
			}
			generate {
				relations = true
				deprecated = false
				records = true
				immutablePojos = false
				fluentSetters = true
			}
			target {
				packageName = 'generated'
				directory = 'src/main/java'
			}
		}
	}
}

flyway {
	url = 'jdbc:postgresql://localhost:5432/homepage'
	user = 'bram'
	password = ''
	schemas = ['public']
}

tasks.generateHomepageJooqSchemaSource.with {
	javaExecSpec = { JavaExecSpec s ->
		s.jvmArgs '--add-modules', 'java.xml.bind'
	}
}

generateHomepageJooqSchemaSource.dependsOn flywayMigrate
test.dependsOn flywayClean
